#---------------------------------------------------------------------------
# Wandering Hamster - see wander.hss
# Lostlandia Garden
#---------------------------------------------------------------------------

plotscript, lostlandia autorun, begin
  set tag(tag:know of lostlandia, true)
  # scrambe berry directions
  scramble npc directions(1)
  if(check tag(tag:frog bridge open)) then(
    open frog bridge(0)
  )
end

plotscript, lostlandia berry bush, arg, ref, begin
  suspend player
  show textbox(1017)
  wait for text box
  variable(who, rank)
  who := pick hero()
  if(who >= 0) then(
    rank := rank in caterpillar(hero by slot(who))
    twirl hero(rank, 2)
    destroy npc(ref)
    lostlandia berry effect(who)
  )
  resume player
end

script, lostlandia berry effect, who, begin
  # Check if this hero has non-full level mp
  if(hero uses level mp(who)) then(
   variable(a)
   a := create array slice(0)
   variable(lmplev, lmax, lcur)
   for(lmplev, 0 , 7) do(
     lmax := get level mp(who, lmplev, maximum stat)
     lcur := get level mp(who, lmplev, current stat)
     if(lmax > 0 && lcur < lmax) then(
       append extra(a, lmplev)
     )
   )
   if(extra length(a) > 0) then(
     lmplev := random array element(a)
     lmax := get level mp(who, lmplev, maximum stat)
     lcur := get level mp(who, lmplev, current stat)
     skill point level display := lmplev + 1
     get hero name(0, who)
     set level mp(who, lmplev, lcur + 1)
     skill point max := lmax
     skill point cur := lcur + 1
     show text box(1018)
     play sound(sfx: potion)
     free slice(a)
     exit()
   )
   free slice(a)
  )
  # Check if the hero has non-full MP
  variable(cur, max, gain)
  cur := get hero stat(who, stat:MP, current stat)
  max := get hero stat(who, stat:MP, maximum stat)
  if(max > 0 && cur < max) then(
    gain := small(random(1, 6), max -- cur)
    set hero stat(who, stat:MP, cur + gain, current stat)
    get hero name(0, who)
    how many for text box := gain
    skill point max := max
    skill point cur := cur + gain
    show text box(1019)
    play sound(sfx: potion)
    exit()
  )
  # restore HP (allowed to exceed max)
  cur := get hero stat(who, stat:HP, current stat)
  max := get hero stat(who, stat:HP, maximum stat)
  cur += 1
  set hero stat(who, stat:HP, cur, current stat)
  get hero name(0, who)
  if(cur <= max) then(
    $1="a little"
  )else(
    $1="weirdly"
  )
  show text box(1020)
end

plotscript, cottonball inn, begin
  suspend player
  set hero speed(me, 5)
  walk hero(me, left, 5)
  suspend caterpillar
  
  pathfind hero to(0, 6, 28)
  pathfind hero to(1, 6, 30)
  pathfind hero to(2, 10, 28)
  pathfind hero to(3, 10, 30)
  wait for hero(0)
  wait for hero(1)
  wait for hero(2)
  wait for hero(3)
  
  play song (song:Sleeping Music)
  tweak palette(-40, -40, -10)
  fade screen in
  wait (18 * 2) # 2 seconds
  key wait seconds(8)
  reset palette
  fade screen in
  play song (get ambient music)
  wait (5)
  
  pathfind hero to(0, 10, 31)
  pathfind hero to(1, 9, 31)
  pathfind hero to(2, 8, 31)
  pathfind hero to(3, 8, 30)
  wait for hero(0)
  wait for hero(1)
  wait for hero(2)
  wait for hero(3)
  
  set hero speed(0, 4)
  resume caterpillar
  resume player
end

plotscript, try to wake up bridge frog, begin
  set tag(tag:yell at bridge frog, ON)
  suspend player
  suspend random enemies
  suspend caterpillar
  suspend obstruction
  pathfind hero to(0, 17, 57)
  pathfind hero to(1, 17, 56)
  pathfind hero to(2, 17, 58)
  pathfind hero to(3, 16, 57)
  wait for hero(0)
  set hero direction(0, right)
  
  show text box(1071) # We want to cross... ZZZZ
  wait for hero(1)
  wait for hero(2)
  wait for hero(3)
  set hero direction(1, right)
  set hero direction(2, right)
  set hero direction(3, right)
  wait for text box
  wait(second / 2)

  show text box(1073) # Hey wake up!... ZZZZ
  wait for text box
  wait(second / 2)
  
  variable(carrot)
  while(true) do(
    show text box(1075)
    wait for text box
    if(check tag(tag:throw carrot at frog)) then(
      if(inventory(item:carrot) > 0) then(
        carrot := create npc(6, 17, 57)
        set npc moves(carrot, false)
        set npc obstructs(carrot, false)
        set npc ignores walls(carrot, true)
        jump npc to(carrot, 20, 56)
        play sound(sfx:crush8bit, false, true)
        jump npc to(carrot, random(19, 21), random(55, 57))
        destroy npc(carrot)
        frog carrots += 1
        delete item(item:carrot)
      )else(
        show text box(1076)
        wait for text box
        if(frog carrots > 0) then(
          show text box(1077)
          wait for text box
        )
        break
      )
    )else(
      break
    )
  )
  
  show text box(1074) # Maybe another way
  wait for text box
  
  resume caterpillar
  pathfind hero to(0, 15, 55)
  wait for hero(0)
  
  resume random enemies
  resume player
  resume obstruction
  set tag(tag:yell at bridge frog, OFF)
end

plotscript, actually wake up bridge frog, begin
  suspend player
  show text box(1081)
  wait for text box
  if(check tag(tag:Gisli in party now)) then(
    show text box(1082)
    wait for text box
  )
  if(check tag(tag:Icefeather in party)) then(
    show text box(1083)
    wait for text box
  )
  if(check tag(tag:Fraidy Plip in party)) then(
    show text box(1084)
    wait for text box
  )
  set tag(tag:woke bridge frog, ON)
  show text box(1085)
  wait for text box
  variable(frog)
  frog := npc reference(4)
  set npc obstructs(frog, false)
  pathfind npc to(frog, 22, 57)
  wait for npc(frog)
  set tag(tag:frog bridge open, ON)
  walk npc(frog, left, 1)
  wait for npc(frog)
  set npc direction(frog, up)
  open frog bridge(2)
  pathfind npc to(frog, 24, 56)
  wait for npc(frog)
  set npc direction(frog, south)
  set npc obstructs(frog, true)
  resume player
end

script, open frog bridge, tickdelay, begin
  variable(x, y, i)
  x := 20
  y := 57
  for(i, 0, 2) do(
    write map block(x -- i, y, 35, 2) # show the bridge
    write pass block(x -- i, y, 0) # erase any walls
    wait(tickdelay)
  )
end

plotscript, give away the fraidy plip, begin
  suspend player
  show text box(1097)
  variable(x, y, d)
  x := hero x(rank in caterpillar(hero:Fraidy Plip))
  y := hero y(rank in caterpillar(hero:Fraidy Plip))
  d := hero direction(rank in caterpillar(hero:Fraidy Plip))
  swap out hero(hero:fraidy plip)
  lock hero(hero:fraidy plip)
  set tag(tag:fraidyplip in house, true)
  variable(plip)
  plip := npc reference(8)
  set npc position(plip, x, y)
  set npc direction(plip, d)
  if(hero x(me) == 9 && hero y(me) == 107) then(
    pathfind hero to(me, 8, 108)
  )
  pathfind npc to(plip, 9, 107)
  wait for hero(me)
  wait for npc(plip)
  set npc direction(plip, down)
  set hero direction(me, right)
  wait for text box

  show text box(1098)
  wait for text box
  
  if(check tag(tag:Icefeather in party)) then(
    show text box(1100)
    wait for text box
  )
  if(check tag(tag:Gisli in party now)) then(
    show text box(1099)
    wait for text box
  )
  
  resume player
end

plotscript, left the plipservationalists house after giving away fraidy plip, begin
  set tag(tag:can borrow fraidy, ON)
end

plotscript, borrow the fraidy plip, begin
  suspend player
  if(room in active party) then(
    show text box(1114)
    wait for text box
    variable(x, y, rank)
    x := npc x(8)
    y := npc y(8)
    suspend caterpillar
    unlock hero(hero:fraidy plip)
    swap in hero(hero:fraidy plip)
    set tag(tag:fraidy plip in house, OFF)
    rank := rank in caterpillar(hero:fraidy plip)
    set hero position(rank, x, y)
    set hero direction(rank, down)
    # heal the fraidy plip
    set hero stat(find hero(hero:fraidy plip), stat:hp, get hero stat(find hero(hero:fraidy plip), stat:hp, maximum stat), current stat) 
    resume caterpillar
  )else(
    show text box(1113)
    wait for text box
  )
  resume player
end

plotscript, return the fraidy plip, begin
  suspend player
  show text box(1097)

  variable(x, y, d)
  x := hero x(rank in caterpillar(hero:Fraidy Plip))
  y := hero y(rank in caterpillar(hero:Fraidy Plip))
  d := hero direction(rank in caterpillar(hero:Fraidy Plip))
  swap out hero(hero:fraidy plip)
  lock hero(hero:fraidy plip)
  set tag(tag:fraidyplip in house, true)
  variable(plip)
  plip := npc reference(8)
  set npc position(plip, x, y)
  set npc direction(plip, d)
  if(hero x(me) == 9 && hero y(me) == 107) then(
    pathfind hero to(me, 8, 108)
  )
  pathfind npc to(plip, 9, 107)
  wait for hero(me)
  wait for npc(plip)
  set npc direction(plip, down)
  set hero direction(me, right)

  wait for text box  
  resume player
end

plotscript, fraidy plip goes home if you try to exit lostlandia, begin
  # This script is run only as a step-on script right before you exit
  # the main entrace of Lostlandia
  if(check tag(tag:gave away fraidy plip)) then(
    if(check tag(tag:fraidy plip in party)) then(
      suspend player
      suspend caterpillar
      show text box(1117)
      variable(plip)
      plip := rank in caterpillar(hero:fraidy plip)
      set hero speed(plip, 8)
      pathfind hero to(plip, 11, 55)
      wait(second / 4)
      variable(i)
      for (i, 0, 3) do(
        set hero direction(i, up)
      )
      wait for hero(plip)
      pathfind hero to(plip, 20, 57)
      wait(second / 4)
      for (i, 0, 3) do(
        set hero direction(i, right)
      )
      wait for hero(plip)

      swap out hero(hero:fraidy plip)
      lock hero(hero:fraidy plip)
      set tag(tag:fraidyplip in house, true)

      wait for text box
      show text box(1118)
      wait for text box
      resume caterpillar
      resume player
    )
  )
end
