#---------------------------------------------------------------------------
# Wandering Hamster - see wander.hss
# Bontgommeri Desert
#---------------------------------------------------------------------------

plotscript, Bontgommeri ruins each step, begin
  variable(x, y)
  x := hero x(me)
  y := hero y(me)
  write sand footprints(x, y)
  if(x == 0 || x == map width -- 1 || y == 0 || y == map height -- 1) then(
    # If you walk to the edge of the map, go to door 10 on the world map
    variable(dx, dy)
    dx := get door x(10, map:World Map)
    dy := get door y(10, map:World Map)
    fade screen out
    teleport to map(map:World Map, dx, dy)
    fade screen in
  )
end

#---------------------------------------------------------------------------

script, write sand footprints, x, y, begin
  if (current map <> map: bontgommeri desert ruins) then(exit script)
  write map block(x, y, 144, 3)
end

#---------------------------------------------------------------------------

plotscript, voguson inn, begin
  suspend player
  pathfind hero to(me, 21, 22, 10)
  wait for hero(me)
  suspend caterpillar
  pathfind hero to(0, 22, 21, 10)
  pathfind hero to(1, 23, 22, 10)
  pathfind hero to(2, 22, 20, 10)
  pathfind hero to(3, 23, 23, 10)
  wait for hero(0)
  wait for hero(1)
  wait for hero(2)
  wait for hero(3)
  
  variable(song)
  song := current song
  play song (song:Sleeping Music)
  tweak palette(-40, -40, -10)
  fade screen in
  key wait seconds(8)
  reset palette
  fade screen in
  play song(song)
 
  resume caterpillar
  resume player
end
