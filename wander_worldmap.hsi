#---------------------------------------------------------------------------
# Wandering Hamster - see wander.hss
# World Map
#---------------------------------------------------------------------------

plotscript,World Map Autorun,begin
 if (check tag(tag:ship initialization)) then(
  # Ship has been initialized, put it in the remembered place
  create npc(2, ship X, ship Y, ship dir)
 )
 tweak bridges
end

#---------------------------------------------------------------------------

plotscript, world map eachstep, begin
  if (current map == map:world map) then(
    if(read zone(10, hero x(me), hero y(me))) then(
      if(check tag(tag:got desert warning)==off) then(
        # Warn the player about the danger of Bontgommeri desert
        show text box(827)
        wait for text box
        set tag(tag:got desert warning, on)
        set tag(tag:know of bontgommeri, on)
      )
    )
    if(read zone(11, hero x(me), hero y(me))) then(
      # Vehicle door to Sandbar map
      suspend player
      fade screen out
      wait for hero(me)
      teleport to map(map:Sandbar, 30, 12)
      set tag(tag:know of sandbar)
      walk hero(me, south, 1)
      wait(1)
      fade screen in
      resume player
    )
  )
end

#---------------------------------------------------------------------------

plotscript,World Map Afterbattle,begin
  default afterbattle
  if (check tag(tag:Riding the Ship)) then,begin
    play song(song:vehicle riding)
  end
  tweak bridges
end

#---------------------------------------------------------------------------

plotscript,Dismount Ship,begin
  ship X := NPC X(2)
  ship Y := NPC Y(2)
  ship dir := NPC direction(2)
  play song(song:overworld music)
end

#---------------------------------------------------------------------------

plotscript,Mount Ship,begin
  play song(song:vehicle riding)
end

#---------------------------------------------------------------------------

plotscript,Dismount Baloon,begin
  baloon X:=NPC X(3)
  baloon Y:=NPC Y(3)
end

#---------------------------------------------------------------------------

plotscript, tweak bridges, flag=0, begin
  if (flag==1) then (under bridge:=true)
  if (flag==2) then (under bridge:=false)
  if (under bridge)
  then(write pass block (144,7,northwall+southwall+overhead tile) )
  else(write pass block (144,7,eastwall+westwall) )
end

#---------------------------------------------------------------------------
